apply plugin: 'java'
//apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.github.johnrengelman.shadow'

group = "com.github.jrachiele"
version = "0.1"

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'com.github.jrachiele:java-timeseries'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Time Series Analysis in Java'
                url 'https://github.com/jrachiele/java-timeseries'

                scm {
                    connection 'scm:git:git://github.com/jrachiele/java-timeseries.git'
                    developerConnection 'scm:git:ssh://github.com/jrachiele/java-timeseries.git'
                    url 'https://github.com/jrachiele/java-timeseries'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        name 'Jacob Rachiele'
                        email 'jrachiele@utexas.edu'
                    }
                }
            }
        }
    }
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

repositories {
    mavenCentral()
}

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.3'
  }
}

task generateJavadoc(type: Javadoc) {
  source = sourceSets.main.allJava
  classpath = sourceSets.main.compileClasspath
  options.memberLevel = JavadocMemberLevel.PUBLIC
  options {
    links "https://docs.oracle.com/javase/8/docs/api/"
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: generateJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

dependencies {

    compile 'org.apache.commons:commons-csv:1.4'
    compile 'com.github.haifengl:smile-core:1.2.0'
    compile 'com.google.guava:guava:20.0'
    compile 'org.knowm.xchart:xchart:3.2.0'
    compile 'org.ejml:all:0.29'
    compile 'org.slf4j:slf4j-simple:1.7.22'

    testCompile 'junit:junit:4.+'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}


